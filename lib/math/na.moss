
begin
global diffh, inv, pli

use math: nan, floor

function diffh(argm={})
  {h=0.001,order=false} = argm
  if order
    return fn diff|f,x,n|
      if n>1
        return (diff(f,x+h,n-1)-diff(f,x-h,n-1))/(2*h)
      elif n==1
        return (f(x+h)-f(x-h))/(2*h)
      else
        return f(x)
      end
    end
  else
    return fn diff|f,x|
      return (f(x+h)-f(x-h))/(2*h)
    end
  end
end

function inv(f,x,a,b)
  a1 = a; b1 = b
  s = sgn(f(b)-f(a))
  if s==0 then s=1 end

  for k in 60
    m = (a+b)/2
    if s*(f(m)-x)<0
      a = m
    else
      b = m
    end
  end
  if abs(m-a1)<1E-8
    return nan
  elif abs(m-b1)<1E-8
    return nan
  else
    return m
  end
end

function pli(x0,d,a)
  n = size(a)
  undefined = nan if n==0 else nan*a[0]
  return fn|x|
    k = int(floor((x-x0)/d))
    if k<0 or k+1>=n
      return undefined
    else
      return (a[k+1]-a[k])/d*(x-x0-k*d)+a[k]
    end
  end
end

end




