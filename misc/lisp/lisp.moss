
use string.syntax: parser, SyntaxError
use sys: argv

ast = parser{
   scan = {symbols = list("()"), letter = "[~{_}()]"},
   transform = {atom = "", expr = "", list = |t| t[1..]},
   grammar = """
      atom = number | identifier;
      list = ('(' {expr}* ')');
      expr = atom | list;
      ast = expr;
   """
}

dispatch = {
   "define": fn|t,env| env.(t[1]) = evaluate(t[2],env) end,
   "lambda": |t,env| |x| evaluate(t[2],table env(map(zip(t[1],x)))),
   "if": (|t,env|
      evaluate(t[3],env) if evaluate(t[1],env) is false else
      evaluate(t[2],env)),
   "let": |t,env| evaluate(t[2],
      table env(map(t[1].map(|[k,v]| [k,evaluate(v,env)]))))
}

global_env = table{
   "+": |a| a.sum(), "*": |a| a.prod(),  
   "-": |a| a[0]-a[1], "/": |a| a[0]/a[1],
   "#t": true, "#f": false,
   "=": |a| a[0]==a[1], "not": |a| a[0] is false,
   "<": |a| a[0]<a[1], "<=": |a| a[0]<=a[1],
   "block": |a| a[-1], "display": |a| print(*a)
}

function evaluate(t,env)
   if t: String
      return env.(t)
   elif t: List
      if t[0]: String and t[0] in dispatch
         return dispatch[t[0]](t,env)
      else
         a = t.map(|x| evaluate(x,env))
         return a[0](a[1..])
      end
   else
      return t
   end
end

function repl()
   hist = []
   while true
      s = input("# ",hist)
      hist.push(s)
      try
         t = ast(s)
      catch e if e: SyntaxError
         print(e.text,"\n")
         continue
      end
      value = evaluate(t,global_env)
      if not value is null
         print(value)
      end
   end
end

function evals(s)
   t = ast(["(block ",s,")"].join())
   value = evaluate(t,global_env)
end

if len(argv)==1
   repl()
else
   evals(read(argv[1]))
end
