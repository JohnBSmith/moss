
use string.syntax: parser, SyntaxError
use sys: argv

ast = parser{
   scan = {symbols = list("()"), letter = "[~{_}()]"},
   transform = {atom = "", expr = "", list = |t| t[1..]},
   grammar = """
      atom = number | identifier;
      list = ('(' {expr}* ')');
      expr = atom | list;
      ast = expr;
   """
}

dispatch = {
   "lambda": |t,con| |x| evaluate(t[2],table con(map(zip(t[1],x)))),
   "if": (|t,con|
      evaluate(t[3],con) if evaluate(t[1],con) is false else
      evaluate(t[2],con)),
   "define": fn|t,con|
      con.(t[1]) = evaluate(t[2],con)
   end
}

global_context = table{
   "+": |a| a.sum(), "*": |a| a.prod(),  
   "-": |a| a[0]-a[1], "/": |a| a[0]/a[1],
   "#t": true, "#f": false,
   "=": |a| a[0]==a[1], "not": |a| a[0] is false,
   "<": |a| a[0]<a[1], "<=": |a| a[0]<=a[1],
   "block": |a| a[-1], "display": |a| print(*a)
}

function evaluate(t,con)
   if t: String
      return con.(t)
   elif t: List
      if t[0]: String and t[0] in dispatch
         return dispatch[t[0]](t,con)
      else
         a = t.map(|x| evaluate(x,con))
         return a[0](a[1..])
      end
   else
      return t
   end
end

function repl()
   hist = []
   while true
      s = input("# ",hist)
      hist.push(s)
      try
         t = ast(s)
      catch e if e: SyntaxError
         print(e.text,"\n")
         continue
      end
      value = evaluate(t,global_context)
      if not value is null
         print(value)
      end
   end
end

function evals(s)
   t = ast(["(block ",s,")"].join())
   value = evaluate(t,global_context)
end

if len(argv)==1
   repl()
else
   evals(read(argv[1]))
end
